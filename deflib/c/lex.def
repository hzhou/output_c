subcode:: _autoload
    $struct(input_buffer) unsigned char * buffer, n_pos
    $register_name(input) struct input_buffer *
    $include ctype
    $register_name(tn_c) int

#---------------------------------------- 
subcode: local_input(s)
    $local struct input_buffer local_input_buffer
    $local input
    input=&local_input_buffer
    input->buffer=(unsigned char*)$(s)
    input->n_pos=0

subcode: local_input_n(s, n)
    $call local_input, $(s)
    $(s)[$(n)] = '\0'

#---------------------------------------- 
subcode: input_get_c(var)
    &call with_input
        $(var) = $(c)
        $(i)++

subcode: input_back_c
    &call with_input
        $call @assert, $(i)>0
        $(i)--

#---- regex captures ------------------
# ref: macros_c/translate_regex.def
subcode:: _autoload
    #-- capture buffers
    $struct(input_buffer) int s[10], int e[10]

subcode: get_ts_group(i)
    ts_group=(char*)(input->buffer+input->s[$(i)])
    $set_var_attr ts_group, strlen=input->e[$(i)]-input->s[$(i)]

subcode: with_ts_group(i)
    #- group as null-terminated string 
    $call get_ts_group, $(i)
    tn_c=input->buffer[input->e[$(i)]]
    input->buffer[input->e[$(i)]] = 0
    BLOCK
    input->buffer[input->e[$(i)]] = tn_c

subcode: get_group(i)
    $local string g$(i)
    g$(i) = input->buffer+input->s[$(i)]
    g$(i)_len = input->e[$(i)] - input->s[$(i)]

#---------------------------------------- 
subcode: match_char_sub(cond)
    $call input_get_c, tn_c
    $if $(cond)
        return 1
    $else
        $if tn_c>0
            $call input_back_c
        return 0

#----
fncode: match_char(tn_char)
    lexical: input
    $return_type bool
    $call match_char_sub, tn_c==tn_char

fncode: match_char_i(tn_char)
    lexical: input
    $return_type bool
    $call match_char_sub, toupper(tn_c)==toupper(tn_char)

#---------------------------------------- 
subcode: match_keyword_sub(notcond)
    $return_type int
    input->s[0]=input->n_pos
    $(set:isword=(isalnum(tn_c)||tn_c=='_'))
    $(set:isword2=(isalnum(tn_c2)||tn_c2=='_'))
    $while 1
        $call input_get_c, tn_c
        $if $(notcond)
            input->n_pos=input->s[0]
            return false
        s_key++
        $if !*s_key
            #-- check word boundary
            $call input_get_c, tn_c2
            $if tn_c2<=0
                return true
            $elif ($(isword) && !$(isword2)) || !$(isword) 
                $call input_back_c
                return true
            $else
                input->n_pos=input->s[0]
                return false

fncode: match_keyword(s_key)
    lexical: input
    $return_type bool
    $call match_keyword_sub, tn_c != *s_key

fncode: match_keyword_i(s_key)    
    lexical: input
    $return_type bool
    $call match_keyword_sub, toupper(tn_c) != toupper(*s_key)

#------------------------------------------
subcode: with_input
    $(set:i=input->n_pos)
    $(set:c=input->buffer[input->n_pos])
    $(set:S=input->s)
    $(set:E=input->e)
    BLOCK

subcode: skip_spaces
    &call with_input
        $while $(c)==' '
            $(i)++

